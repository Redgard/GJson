$namespace=GJson

COMPILER Json

CHARACTERS

    digit    = "0123456789".
    digitH   = "123456789".
    hexCh    = "0123456789ABCDEFabcdef".
    cr       = '\r'.
    lf       = '\n'.
    tab      = '\t'.
    stringCh = ANY - '"' - '\\' - cr - lf.
    escCh    = "\"\\/bfnrt".
	
TOKENS

    number = [ '-' ] ('0' | digitH { digit }) [ '.' { digit } ] [ ('e' | 'E') [ '+' | '-' ] digit { digit } ].
    string = '"' { stringCh | '\\' escCh | "\\u" hexCh hexCh hexCh hexCh } '"'.
	
COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf
	
IGNORE cr + lf + tab

PRODUCTIONS

Json =
	Value.

String = string (. Replace<string>( t.val ); .).
	
Object = (. ReplaceObject(); .)
	'{' [ ItemList ] '}'.

ItemList =
	Item { ',' Item }.

Item =
	(. PushEmpty(); .) String
	':' Value (. AddItemToObject(); .) .

Array = (. ReplaceArray(); .)
	'[' [ ValueList ] ']'.

ValueList =
	Value (. AddItemToArray(); .)
	{ ',' Value (. AddItemToArray(); .) }.

Value = (. PushEmpty(); .)
( Object
 | Array
 | String
 | number (. Replace<double>( t.val ); .)
 | "true" (. Replace<bool>( t.val ); .)
 | "false" (. Replace<bool>( t.val ); .)
 | "null" (. ReplaceEmpty(); .) ) .

END Json.