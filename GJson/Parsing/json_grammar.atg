$namespace=GJson

COMPILER Json

CHARACTERS

    digit    = "0123456789".
    digitH   = "123456789".
    hexCh    = "0123456789ABCDEFabcdef".
    cr       = '\r'.
    lf       = '\n'.
    tab      = '\t'.
    stringCh = ANY - '"' - '\\' - cr - lf.
    escCh    = "\"\\/bfnrt".
	
TOKENS

    TNumber = [ '-' ] ('0' | digitH { digit }) [ '.' { digit } ] [ ('e' | 'E') [ '+' | '-' ] digit { digit } ].
    TString = '"' { stringCh | '\\' escCh | "\\u" hexCh hexCh hexCh hexCh } '"'.
	TNull = "null".
	TTrue = "true".
	TFalse = "false".
	QuotationMark = "\"".
	CurlyBracketOpen = "{".
	CurlyBracketClose = "}".
	SquareBracketOpen = "[".
	SquareBracketClose = "]".
	Colon = ":".
	Comma = ",".
	Tab = "\t".

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf
	
IGNORE cr + lf + tab

PRODUCTIONS

Json =
	Value.

String =
	TString .
	
Number =
	TNumber . 

True =
	TTrue . 

False =
	TFalse . 

Null =
	TNull . 

Object =
	CurlyBracketOpen [ ObjectList ] CurlyBracketClose .
	
ObjectList =
	ObjectItem { Comma ObjectItem } .

ObjectItem =
	String Colon Value .

Array =
	SquareBracketOpen [ ArrayList ] SquareBracketClose .

ArrayList =
	ArrayItem { Comma ArrayItem } .

ArrayItem = 
	Value .

Value = 
	Object
	| Array
	| String
	| Number
	| True
	| False 
	| Null .

END Json.
